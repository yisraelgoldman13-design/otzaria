; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "אוצריא"
#define MyAppVersion "0.9.71"
#define MyAppPublisher "sivan22"
#define MyAppURL "https://github.com/Y-PLONI/otzaria"
#define MyAppExeName "otzaria.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{EEC4F712-CD05-4D15-A753-509E840A51A5}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
PrivilegesRequired=admin
OutputDir=.
OutputBaseFilename=otzaria-{#MyAppVersion}-windows-full
SetupIconFile=white_sketch128x128.ico
Compression=lzma
SolidCompression=yes
; Disable compression for DLL files to prevent corruption
CompressionThreads=1
WizardStyle=modern
DisableDirPage=no

[InstallDelete]
; מחיקת ספרייה ישנה לפני חילוץ הZIP החדש
Type: filesandordirs; Name: "{app}\אוצריא"
Type: filesandordirs; Name: "{app}\links"

[Languages]
Name: "hebrew"; MessagesFile: "compiler:Languages\Hebrew.isl"

[Code]
function InitializeSetup(): Boolean;
var
  InstallPath, OldPath: String;
begin
  Result := True;
  InstallPath := ExpandConstant('C:\{#MyAppName}');
  OldPath := 'C:\אוצריא';
  
  // בדיקה אם יש התקנה ישנה בנתיב העברי
  if DirExists(OldPath) then
  begin
    if MsgBox('נמצאה התקנה ישנה ב-' + OldPath + #13#10 +
              'התוכנה עברה לנתיב חדש: ' + InstallPath + #13#10#13#10 +
              'האם להעביר את הנתונים למיקום החדש?',
              mbConfirmation, MB_YESNO) = IDYES then
    begin
      // המשתמש יצטרך להעביר ידנית או שנוסיף קוד העברה
      MsgBox('לאחר ההתקנה, תוכל להעביר את הנתונים מ-' + OldPath + ' ל-' + InstallPath, mbInformation, MB_OK);
    end;
  end;
  
  // בדיקה אם התיקיות קיימות והצגת אזהרה
  if DirExists(InstallPath + '\אוצריא') or DirExists(InstallPath + '\links') then
  begin
    if MsgBox('שים לב: אם קיימים נתונים בתיקיות הבאות, הם ימחקו:' + #13#10 +
              '- אוצריא' + #13#10 +
              '- links' + #13#10#13#10 +
              'האם להמשיך בהתקנה?',
              mbConfirmation, MB_YESNO) = IDNO then
    begin
      Result := False;
    end;
  end;
end;

function VCRedistNeedsInstall: Boolean;
var
  Version: String;
begin
  // Check if Visual C++ 2015-2022 Redistributable is installed
  // This checks for x64 version
  Result := not RegQueryStringValue(HKLM64, 'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x64', 
    'Version', Version);
  if not Result then
  begin
    // Also check x86 version
    Result := not RegQueryStringValue(HKLM64, 'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x86', 
      'Version', Version);
  end;
end;

// הפונקציה הזו כבר לא נחוצה - הקבצים מועתקים ישירות על ידי Inno Setup
// procedure CurStepChanged(CurStep: TSetupStep);
// begin
//   // Library files are now copied directly by Inno Setup [Files] section
// end;

[Run]
Filename: "{tmp}\VisualCppRedist_AIO_x86_x64.exe"; Parameters: "/ai /gm2"; StatusMsg: "מתקין Visual C++ Redistributable..."; Flags: waituntilterminated; Check: VCRedistNeedsInstall
Filename: "C:\Windows\System32\WindowsPowerShell\v1.0\powershell.exe"; WorkingDir: "{app}"; Parameters: " -sta -WindowStyle Hidden -noprofile -executionpolicy bypass -file uninstall_msix.ps1"; Flags: runhidden waituntilterminated
Filename: "{app}\{#MyAppExeName}"; Description: "הפעל את {#MyAppName}"; Flags: nowait postinstall skipifsilent 

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
; Copy DLL files without compression to prevent corruption
Source: "..\build\windows\x64\runner\Release\*.dll"; DestDir: "{app}"; Flags: ignoreversion nocompression
; Copy all other app files
Source: "..\build\windows\x64\runner\Release\*"; \
    Excludes: "*.msix,*.msixbundle,*.appx,*.appxbundle,*.appinstaller,*.dll"; \
    DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

; Copy extracted library files directly (no ZIP extraction needed!)
Source: "library_files\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "uninstall_msix.ps1"; DestDir: "{app}"; Flags: ignoreversion
Source: "VisualCppRedist_AIO_x86_x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
