; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "אוצריא"
#define MyAppVersion "0.9.69"
#define MyAppPublisher "sivan22"
#define MyAppURL "https://github.com/Y-PLONI/otzaria"
#define MyAppExeName "otzaria.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{EEC4F712-CD05-4D15-A753-509E840A51A5}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
PrivilegesRequired=admin
OutputDir=.
OutputBaseFilename=otzaria-{#MyAppVersion}-windows-full
SetupIconFile=white_sketch128x128.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern
DisableDirPage=auto

[InstallDelete]
; מחיקת ספרייה ישנה לפני חילוץ הZIP החדש
Type: filesandordirs; Name: "{app}\אוצריא"
Type: filesandordirs; Name: "{app}\links"

[Languages]
Name: "hebrew"; MessagesFile: "compiler:Languages\Hebrew.isl"

[Code]
function VCRedistNeedsInstall: Boolean;
var
  Version: String;
begin
  // Check if Visual C++ 2015-2022 Redistributable is installed
  // This checks for x64 version
  Result := not RegQueryStringValue(HKLM64, 'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x64', 
    'Version', Version);
  if not Result then
  begin
    // Also check x86 version
    Result := not RegQueryStringValue(HKLM64, 'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x86', 
      'Version', Version);
  end;
end;

function ExtractLibrary: Boolean;
var
  ResultCode: Integer;
  ZipPath, ExtractPath: String;
begin
  Result := True;
  ZipPath := ExpandConstant('{tmp}\otzaria_latest.zip');
  ExtractPath := ExpandConstant('{app}');
  
  // חילוץ קובץ ZIP שנמצא במתקין
  WizardForm.StatusLabel.Caption := 'מחלץ את ספריית האוצריא...';
  try
    if not Exec('powershell.exe',
      '-NoProfile -Command "& {Expand-Archive -Path ''' + ZipPath + ''' -DestinationPath ''' + ExtractPath + ''' -Force}"',
      '', SW_HIDE, ewWaitUntilTerminated, ResultCode) or (ResultCode <> 0) then
    begin
      MsgBox('שגיאה בחילוץ הספרייה. קובץ הZIP אולי פגום.', mbError, MB_OK);
      Result := False;
    end;
  except
    Result := False;
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
  begin
    ExtractLibrary;
  end;
end;

[Run]
Filename: "{tmp}\VisualCppRedist_AIO_x86_x64.exe"; Parameters: "/ai /gm2"; StatusMsg: "מתקין Visual C++ Redistributable..."; Flags: waituntilterminated; Check: VCRedistNeedsInstall
Filename: "C:\Windows\System32\WindowsPowerShell\v1.0\powershell.exe"; WorkingDir: "{app}"; Parameters: " -sta -WindowStyle Hidden -noprofile -executionpolicy bypass -file uninstall_msix.ps1"; 

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\build\windows\x64\runner\Release\*"; \
    Excludes: "*.msix,*.msixbundle,*.appx,*.appxbundle,*.appinstaller"; \
    DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

; קובץ ZIP של הספרייה - יורד על ידי הworkflow לשורש הפרויקט
Source: "..\otzaria_latest.zip"; DestDir: "{tmp}"; Flags: deleteafterinstall

Source: "uninstall_msix.ps1"; DestDir: "{app}"; Flags: ignoreversion
Source: "VisualCppRedist_AIO_x86_x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
